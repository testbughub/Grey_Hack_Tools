if params.len != 2 or params[0] == "-h" or params[0] == "--help" then exit("<b>Usage: exploit [ip_address] [port]</b>")
metaxploit = include_lib("/lib/metaxploit.so")
if not metaxploit then
    currentPath = get_shell.host_computer.current_path
    metaxploit = include_lib(currentPath + "/metaxploit.so")
end if
if not metaxploit then exit("Error: Can't find metaxploit library in the /lib path or the current folder")
address = params[0]
port = params[1].to_int
net_session = metaxploit.net_use( address, port )
if not net_session then exit("Error: can't connect to net session")
metaLib = net_session.dump_lib
result = metaLib.overflow("0x32C1FE", "eatoreadeintl")
if not result then exit("Program ended")
cryptools = include_lib("/lib/crypto.so")
if not cryptools then
	currentPath = get_shell.host_computer.current_path
	cryptools = include_lib(currentPath + "/crypto.so")
end if
if not cryptools then exit("Error: Can't find crypto.so library in the /lib path or the current folder")

GetPassword = function(userPass)
	if userPass.len != 2 then exit("decipher: wrong syntax")
	password = cryptools.decipher(userPass[0], userPass[1])
	return password
end function

typeObject = typeof(result)
if(typeObject != "computer") then exit("Error: expected computer, obtained " + typeObject)
homeFolder = result.File("/home")
if not homeFolder then exit("Error: /home folder not found")
userFolders = homeFolder.get_folders
found = false
for userFolder in userFolders
	bankFile = result.File("/home/" + userFolder.name + "/Config/Bank.txt")
	if not bankFile then continue
	if not bankFile.has_permission("r") then exit("Error: can't read file contents. Permission deniend")
	userPass = bankFile.content.split(":")
	print("Deciphering bank password for user: " + userFolder.name)
	password = GetPassword(userPass)
	if not password then
		print("Nothing found...")
	else
		print("Bank account: " + userPass[0] +"\nBank Password: " + password)
		found = true
	end if
end for
if not found then print("No files found")
